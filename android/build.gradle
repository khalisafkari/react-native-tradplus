buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"


def appProject = rootProject.allprojects.find { it.plugins.hasPlugin('com.android.application') }

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["Tradplus_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["Tradplus_" + name]).toInteger()
}

def isAGPVersionGreaterThan(version) {
  def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')[0].toInteger()
  return agpVersion > version
}

android {
  if (isAGPVersionGreaterThan(7)) {
    namespace "com.tradplus"
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }
  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

}

repositories {
  mavenCentral()
  google()
}


dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  //implementation "com.facebook.react:react-native:+"

     implementation "com.facebook.react:react-android:+"

     // TradPlus
     implementation 'androidx.legacy:legacy-support-v4:1.0.0'
     implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
     // TradPlus
     implementation 'com.tradplusad:tradplus:9.8.0.1'
    // Admob
     implementation 'com.google.android.gms:play-services-ads:22.1.0'
     implementation 'com.tradplusad:tradplus-googlex:2.9.8.0.1'
    // Meta
     implementation 'com.facebook.android:audience-network-sdk:6.14.0'
     implementation 'com.tradplusad:tradplus-facebook:1.9.8.0.1'
    // Cross Promotion
     implementation 'com.tradplusad:tradplus-crosspromotion:27.9.8.0.1'
    // Adx
     implementation 'com.google.code.gson:gson:2.8.6'
     implementation 'com.tradplusad:tp_exchange:40.9.8.10.1'
     implementation 'com.tradplusad:om-sdk:1.4.4'

}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "Tradplus"
    codegenJavaPackageName = "com.tradplus"
  }
}
